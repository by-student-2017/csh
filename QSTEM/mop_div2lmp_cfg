#! /bin/csh -f
set filename = $1
#
set element_list = (H He Li Be B C N O F Ne Na Mg Al Si P S Cl Ar K Ca Sc Ti V Cr Mn Fe Co Ni Cu Zn Ga Ge As Se Br Kr Rb Sr Y Zr Nb Mo Tc Ru Rh Pd Ag Cd In Sn Sb Te I Xe Cs Ba La Ce Pr Nd Pm Sm Eu Gd Tb Dy Ho Er Tm Yb Lu Hf Ta W Re Os Ir Pt Au Hg Tl Pb Bi Po At Rn Fr Ra Ac Th Pa U Np Pu Am Cm Bk Cf Es Fm Md No Lr Rf Df Sg Bh Hs Mt Ds Rg Cn Nh Fl Mc Lv Ts Og)
#
set mass_list = (1.00794 4.00260 6.941 9.01218 10.81 12.0107 14.007 16.00 18.9984 20.180 22.99 24.305 26.98 28.1 30.97 32.1 35.45 39.95 39.10 40.08 44.955912 47.867 50.9415 51.9961 54.938045 55.845 58.933195 58.6934 63.546 65.38 69.723 72.63 74.92160 78.96 79.904 83.798 85.4678 87.62 88.90585 91.224 92.90638 95.96 98 101.07 102.90550 106.42 107.8682 112.411 114.818 118.710 121.760 127.60 126.90447 131.293 132.9054519 137.33 138.90547 140.116 140.90765 144.242 145 150.36 151.964 157.25 158.92535 162.500 164.93032 167.259 168.93421 173.054 174.9668 178.49 180.94788 183.84 186.207 190.23 192.217 195.084 196.966569 200.59 204.3833 207.2 208.98040 209 210 222 223 226 227 232.0381 231.03588 238.02891 237 244 243 247 247 251 252 257 258 259 262 261.11 268 271 270 269 278 281 281 285 286 289 289 293 294 294)
#
cp ${filename}.para tmp.para
set a = `awk 'NR==1{print $1}' tmp.para`
echo "a lattice constant:" ${a}
set b = `awk 'NR==2{print $1}' tmp.para`
echo "b lattice constant:" ${b}
set c = `awk 'NR==3{print $1}' tmp.para`
echo "c lattice constant:" ${c}
#
set numofpar = 23040
echo "Number of particles = "${numofpar} > tmp.cfg
echo "A = 1.0 Angstrom (basic length-scale)"  >> tmp.cfg
echo "H0(1,1) = "${a}" A" >> tmp.cfg
echo "H0(1,2) = 0.0 A" >> tmp.cfg
echo "H0(1,3) = 0.0 A" >> tmp.cfg
echo "H0(2,1) = 0.0 A" >> tmp.cfg
echo "H0(2,2) = "${b}" A" >> tmp.cfg
echo "H0(2,3) = 0.0 A" >> tmp.cfg
echo "H0(3,1) = 0.0 A" >> tmp.cfg
echo "H0(3,2) = 0.0 A" >> tmp.cfg
echo "H0(3,3) = "${c}" A" >> tmp.cfg
echo ".NO_VELOCITY." >> tmp.cfg
echo "entry_count = 3" >> tmp.cfg
#
set MaxNo = $2
set i = 1
while ($i <= $MaxNo)
  set j = 1 
  while ($j <= $MaxNo)
    set k = 1 
    while ($k <= $MaxNo)
      echo " "
      echo $i$j$k
      #
      set end_line = `awk 'END{print NR}' ${filename}_$i$j$k.mop`
      if ($end_line >= 4) then
        #
        cp ${filename}_$i$j$k.out tmp_check.out
        set check = `grep -c "  UNABLE TO ACHIEVE SELF-CONSISTENCE" tmp_check.out`
        if ("$check" == "0") then
          cat ${filename}_$i$j$k.out | tac | sed '/SCF FIELD WAS ACHIEVED/q' | tac > tmp.out
          #
          set read_start = `grep -n "CARTESIAN COORDINATES" tmp.out | sed -e 's/:.*//g'`
          set read_end   = `grep -n "Empirical Formula:" tmp.out | sed -e 's/:.*//g'`
          #set total_atoms = `awk '{if($1=="Empirical" && $2=="Formula:"){print $6}}' tmp.out`
          #
          set m = 1
          foreach atom (${element_list})
            awk -v rs=${read_start} -v re=${read_end} -v mass=$mass_list[$m] -v atomic_symbol=${atom} -v n=2 -v a=${a} -v b=${b} -v c=${c} '(NR>=rs && NR<=re && $2==atomic_symbol){printf "%-12.6f \n%s \n%-12.6f %-12.6f %-12.6f \n",mass,$2,$3/a,$4/b,$5/c}' tmp.out >> tmp.cfg
            @ m = $m + 1
          end
        else
          cat ${filename}_$i$j$k.out | tac | sed '/  UNABLE TO ACHIEVE SELF-CONSISTENCE/q' | tac > tmp.out
          #
          set read_start = `grep -n "  UNABLE TO ACHIEVE SELF-CONSISTENCE" tmp.out | sed -e 's/:.*//g'`
          set read_end   = `grep -n "* UNABLE TO ACHIEVE SELF-CONSISTENCE" tmp.out | sed -e 's/:.*//g'`
          #set total_atoms = `awk '{if($1=="Empirical" && $2=="Formula:"){print $6}}' tmp.out`
          #
          set m = 1
          foreach atom (${element_list})
            awk -v rs=${read_start} -v re=${read_end} -v mass=$mass_list[$m] -v atomic_symbol=${atom} -v n=2 -v a=${a} -v b=${b} -v c=${c} '(NR>=rs && NR<=re && $1==atomic_symbol){printf "%-12.6f \n%s \n%-12.6f %-12.6f %-12.6f \n",mass,$1,$2/a,$4/b,$6/c}' tmp.out >> tmp.cfg
            @ m = $m + 1
          end
        endif
        #
      endif
      @ k = $k + 1
    end
    @ j = $j + 1
  end
  @ i = $i + 1
end
#
set total_number_of_atom = `awk 'END{print (NR-13)/3}' tmp.cfg`
sed -i -e "1c\Number of particles = ${total_number_of_atom}" tmp.cfg
mv tmp.cfg ${filename}.lmp_cfg
rm -f -r tmp.out
