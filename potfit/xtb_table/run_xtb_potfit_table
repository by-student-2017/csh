#! /bin/csh -f

set num_core_pc = 2
set ncore = `echo ${num_core_pc} | awk '{printf "%d",sqrt($0)}'`
set num_core_potfit = 0 # less than number of data in config. 0: auto 

set filename = $1

echo -n "" > ${filename}.config

echo "| Structure | Nc | Nat | W | Tmin-Tmax | Vmin-Vmax | Nmd |" > output_table
echo "----------------------------------------------------------" >> output_table
echo "Configurations with attributed forces (MD calculation)" >> output_table
set SCF_flag = 0

set num_data = 0
set poscar_list = `grep "\.poscar" table | sed "s/.poscar.*//g"`
foreach poscar_name_pre (${poscar_list})
  # table data
  set poscar_name = ${poscar_name_pre}.poscar
  set weight = `grep ${poscar_name} table | awk '{printf "%d",$2}'`
  set Tmin   = `grep ${poscar_name} table | awk '{printf "%7.2f",$3}'`
  set Tmax   = `grep ${poscar_name} table | awk '{printf "%7.2f",$4}'` 
  set NT     = `grep ${poscar_name} table | awk '{printf "%1d",$5}'` 
  set Vmax   = `grep ${poscar_name} table | awk '{printf "%8.5f",($6/100)*(1/3)}'`
  set NV     = `grep ${poscar_name} table | awk '{printf "%d",$7}'`
  set F_flag = `grep ${poscar_name} table | awk '{printf "%d",$8}'`
  set Nmd    = `grep ${poscar_name} table | awk '{printf "%d",$9}'`
  #
  set num_data = `echo "${num_data}+${NT}*${NV}" | bc`
  #
  set poscar_folder_name = `echo $poscar_name:r`

  set i = 1
  while($i <= ${NV})
    set scale = `echo "1.00 - ${Vmax}  + ${Vmax}/((${NV}+1)/2)*$i" | bc -l | awk '{printf "%5.3f",$0}'` 
    set j = 1
    while($j <= ${NT}) 
      set temp = `echo "${Tmin} + (${Tmax}-${Tmin})/${NT}*($j-1)" | bc -l | awk '{printf "%7.2f",$0}'`
      rm -f -r ${poscar_folder_name}_L${scale}_${temp}K
      mkdir ${poscar_folder_name}_L${scale}_${temp}K
      cp ${poscar_name} ./${poscar_folder_name}_L${scale}_${temp}K/${poscar_name}
      cd ./${poscar_folder_name}_L${scale}_${temp}K

      if (${F_flag} == 1) then
        cp ../xcontrol ./xcontrol
        sed -i "s/298.15/${temp}/g" xcontrol
        cp ../xcontrol_restart ./xcontrol_restart
        sed -i "s/298.15/${temp}/g" xcontrol_restart
        cp ../xtb2force ./xtb2force
      endif

      cp ../${poscar_name} POSCAR_tmp
      awk -v scale=${scale} '{if(NR==2){printf " %19.15f \n",$1*scale}else{printf "%s \n", $0}}' POSCAR_tmp > ${poscar_name}

      if (${F_flag} == 1) then
        #xtb ${poscar_name} --periodic --md --input xcontrol
        ../run_md.csh ${poscar_name} ${Nmd}
        mv config_all config
      else
        xtb ${poscar_name} --periodic --opt
        xtb ${poscar_name} --periodic --restart --grad
        ../xtb2force
      endif

      #./xtb2force
      mv config ${poscar_folder_name}_L${scale}_${temp}K.config

      if (${F_flag} == 1) then
        cp ${poscar_folder_name}_L${scale}_${temp}K.config new_${poscar_folder_name}_L${scale}_${temp}K.config
      else
        awk '{if(NR==1){printf "#N %d 0 \n",$2}else{printf "%s \n",$0}}'  ${poscar_folder_name}_L${scale}_${temp}K.config > new_${poscar_folder_name}_L${scale}_${temp}K.config
      endif
      sed -i "s/#W 1.00000/#W ${weight}/g" new_${poscar_folder_name}_L${scale}_${temp}K.config
      mv new_${poscar_folder_name}_L${scale}_${temp}K.config ${poscar_folder_name}_L${scale}_${temp}K.config
      cat ${poscar_folder_name}_L${scale}_${temp}K.config >> ../${filename}.config
    
      cd ..
      @ j = $j + 1
    end
    #
    if ($i == 1) then
      set Vmin = `awk '{if($1=="volume/ion"){printf "%4.1f \n",$5}}' ./${poscar_folder_name}_L${scale}_${temp}K/OUTCAR`
    endif
    if ($i == ${NV}) then
      set Vmax = `awk '{if($1=="volume/ion"){printf "%4.1f \n",$5}}' ./${poscar_folder_name}_L${scale}_${temp}K/OUTCAR`
    endif
    #
    @ i = $i + 1
  end
  #
  if ($SCF_flag == 0 && $F_flag == 0) then
    echo " " >> output_table
    echo "Configurations without attributed forces (SCF calculation)" >> output_table
    set SCF_flag = 1
  endif
  set Nc = `echo "${NT} * ${NV}" | bc`
  set Nat_all = `grep . ${poscar_name} | wc -l`
  set Nat = `echo "${Nat_all}-8" | bc`
  echo "${poscar_name}   ${Nc}  ${Nat}   ${weight}   ${Tmin}-${Tmax}   ${Vmin}-${Vmax}   ${Nmd}" >> output_table
  #
end

$HOME/potfit/util/correct_config ${filename}
set arr = `sed -n 2p ${filename}.config | sed "s/#C //g" | sed 's/\s*$//'`
set array = `echo "($arr)"`
set total_num_elements = `echo $#array`
echo ${total_num_elements}
#set new_filename = `echo ${arr} | sed "s/ /-/g"`

if ("${total_num_elements}" == "1") then
  if ("${potential_type}" == "eam") then
    makeapot -g -n 1 -i eam -f "morse_sc,csw2_sc,bjs" | tee ${filename}.pot
  else if ("${potential_type}" == "adp") then
    makeapot -g -n 1 -i adp -f "eopp_sc,csw2_sc,bjs,2*csw2_sc" | tee ${filename}.pot
  #else if ("${potential_type}" == "stiweb") then
  #  makeapot -g -n 1 -i stiweb | tee ${filename}.pot
  #else if ("${potential_type}" == "tersoff") then
  #  makeapot -g -n 1 -i tersoff | tee ${filename}.pot
  endif
else if ("${total_num_elements}" == "2") then
  if ("${potential_type}" == "eam") then
    makeapot -g -n 2 -i eam -f "3*morse_sc,2*csw2_sc,2*bjs" | tee ${filename}.pot
  else if ("${potential_type}" == "adp") then
    makeapot -g -n 2 -i adp -f "3*eopp_sc,2*csw2_sc,2*bjs,6*csw2_sc" | tee ${filename}.pot
  #else if ("${potential_type}" == "stiweb") then
  #  makeapot -g -n 2 -i stiweb | tee ${filename}.pot
  #else if ("${potential_type}" == "tersoff") then
  #  makeapot -g -n 2 -i tersoff | tee ${filename}.pot
  endif
else if ("${total_num_elements}" == "3") then
  if ("${potential_type}" == "eam") then
    makeapot -g -n 3 -i eam -f "6*morse,3*csw2,3*bjs" | tee ${filename}.pot
  else if ("${potential_type}" == "adp") then
    makeapot -g -n 3 -i adp -f "6*eopp_sc,3*csw2_sc,3*bjs,12*csw2_sc" | tee ${filename}.pot
  #else if ("${potential_type}" == "stiweb") then
  #  makeapot -g -n 3 -i stiweb | tee ${filename}.pot
  #else if ("${potential_type}" == "tersoff") then
  #  makeapot -g -n 3 -i tersoff | tee ${filename}.pot
  endif
endif

potfit_setup -c ${filename}.config -p ${filename}.pot -s ${filename} | tee param_file
if ("${num_core_potfit}" == "0") then
  set i = 1
  while($i <= $num_core_pc)
    set mod = `echo "${num_data} % $i" | bc`
    if("$mod" == "0") then
      set num_core_potfit = $i
    endif
  @ i = $i + 1
  end
endif
if ("${num_core_potfit}" == "1") then
  potfit_apot_${potential_type}_mkl_stress param_file
else
  mpirun -np ${num_core_potfit} potfit_apot_${potential_type}_mkl_mpi_stress param_file
endif

